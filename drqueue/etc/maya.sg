#
# THIS IS A TCSH SCRIPT FILE
# 
# Default configuration for maya script generator
# You can set the environment variables here (csh style)
# setupenv -ver 4.0 maya

#
# For platform dependend environment setting a form like this
# can be used :
#
#
# if ($DRQUEUE_OS == "LINUX") then
#    # Environment for Linux
# else if ($DRQUEUE_OS == "IRIX") then
#    # Environment for Irix
# else
#    # Some error messages
# endif
#
# The same structure can be used with the Render command
#
#

if ($DRQUEUE_OS == "WINDOWS") then
  set BLOCK=`"$DRQUEUE_BIN/expr.exe" $DRQUEUE_FRAME + $DRQUEUE_BLOCKSIZE - 1`
else
  set BLOCK=`expr $DRQUEUE_FRAME + $DRQUEUE_BLOCKSIZE - 1`
endif
if ($BLOCK > $DRQUEUE_ENDFRAME) then
  set BLOCK = $DRQUEUE_ENDFRAME;
endif

if ($?DRQUEUE_IMAGE) then
  set CIMAGE = "-im $DRQUEUE_IMAGE"
else
  set CIMAGE = ""
endif

if ($?CAMERA) then
  set CAMERA = "-cam $CAMERA"
else
  set CAMERA = ""
endif

if ($?RESX) then
  set RESX = "-x $RESX"
else
  set RESX = ""
endif

if ($?RESY) then
  set RESY = "-y $RESY"
else
  set RESY = ""
endif

if ($?FFORMAT) then
  set FFORMAT = "-of $FFORMAT"
else
  set FFORMAT = ""
endif

if ($?MENTALRAY) then
	set MENTALRAY = "-r mr"
else
	set MENTALRAY = ""
endif

echo render -s $DRQUEUE_FRAME -e $BLOCK $RESX $RESY $FFORMAT -rd "$DRQUEUE_RD" $MENTALRAY $CIMAGE $CAMERA "$DRQUEUE_SCENE"

if ($DRQUEUE_OS == "WINDOWS") then
  render -s $DRQUEUE_FRAME -e $BLOCK $RESX $RESY $FFORMAT -rd "$DRQUEUE_RD" $MENTALRAY $CIMAGE $CAMERA "$DRQUEUE_SCENE"
else
  Render -s $DRQUEUE_FRAME -e $BLOCK $RESX $RESY $FFORMAT -rd $DRQUEUE_RD $MENTALRAY $CIMAGE $CAMERA $DRQUEUE_SCENE
endif

# This should requeue the frame if failed
if ( $? != 0 ) then
  echo "Requeueing frame..."
  kill -INT $$
else
  # The frame was rendered properly
  $DRQUEUE_BIN/chown_block $RF_OWNER $DRQUEUE_RD/$DRQUEUE_IMAGE $DRQUEUE_FRAME $BLOCK 
endif

#
# Notice that the exit code of the last command is received by DrQueue
#
